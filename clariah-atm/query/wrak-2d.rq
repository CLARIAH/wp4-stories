prefix das: <https://demo.triply.cc/huygens-ing/dutch-asiatic-shipping/>
prefix dce: <http://purl.org/dc/elements/1.1/>
prefix edm: <http://www.europeana.eu/schemas/edm/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix misc: <https://demo.triply.cc/huygens-ing/graph/misc/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix schema: <http://schema.org/>
prefix ship: <https://demo.triply.cc/huygens-ing/dutch-asiatic-shipping/id/ship/>
prefix wgs84: <http://www.w3.org/2003/01/geo/wgs84_pos#>
select * {
  bind(ship:DAS_ship0010 as ?ship)# 't Vliegend Hart' == DAS_ship0010
  ?ship das:shipname ?shipName.
  ?voyage
    das:shipid ?ship;
    das:voydepartureedtf ?departureDate;
    das:voydepartureplaceid [
      das:toponym_original ?departureName;
      owl:sameAs [
        wgs84:long ?departureLong;
        wgs84:lat ?departureLat]].
  optional {
    [ dce:creator ?departureImageCreator;
      dce:description ?departureImageDesc;
      foaf:depiction ?departureImage1;
      misc:place ?departureName;
      misc:year ?departureImageYear].
    optional {
      [ edm:isShownAt ?departureImage1;
        edm:isShownBy ?departureImage2;
        edm:provider ?provider].
    }
    bind(if(bound(?departureImage1)&&strends(str(?departureImage1),'.jpg'),
            ?departureImage1,
            ?departureImage2) as ?departureImageUrl)
  }
  bind(if(bound(?departureImageUrl),'<img src="{{departureImageUrl}}" style="width:300px;height:300px;">','') as ?departureImg)
  filter not exists{ ?voyage das:voyarrivalplace ?arrival. }
  optional {
    ?wreck
      schema:name ?shipName;
      misc:lat ?wreckLat;
      misc:lon ?wreckLong;
      misc:locatie ?locatie;
      misc:historie ?description.
  }
  bind(strdt(concat('Point(',?departureLong,' ',?departureLat,')'),geo:wktLiteral) as ?departureShape)
  bind(strdt(concat('Point(',?wreckLong,' ',?wreckLat,')'),geo:wktLiteral) as ?wreckShape)
  bind(str(concat('Polygon((',?wreckLong,' ',?wreckLat,',',?departureLong,' ',?departureLat,'))')) as ?routeShape).
  bind('''
       <h3>{{departureName}}</h3>
       <p>Vertrokken op {{departureDate}}</p>
       <figure>
         {{departureImg}}
         <figcaption>
           “{{departureImageDesc}}” door {{departureImageCreator}}, {{departureImageYear}} [Herkomstcollectie: {{provider}}].
         </figcaption>
       </figure>
       ''' as ?departureShapeLabel)
  bind('<p>Gezonken op: {{locatie}}, {{description}}</p>' as ?wreckShapeLabel)
}

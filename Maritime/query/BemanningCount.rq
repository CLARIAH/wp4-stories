prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix na: <http://archief.nl/def/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix opvarenden: <https://demo.triply.cc/huygens-ing/voc-opvarenden-verrijkingen-carrieres/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix voyage-na: <http://archief.nl/NL-HaNA/id/reis/VoyageDas:>
prefix voyage-huygens: <https://demo.triply.cc/huygens-ing/voc-opvarenden-verrijkingen-carrieres/id/dasTrip/>
select ?shape ?shapeColor ?shapeLabel {
  bind(voyage-na:2978.2 as ?trip1)
  bind(voyage-huygens:2978.2 as ?trip2)
  ?trip1
    na:voyageHasPassenger [
      na:hasProfession ?beroep;
      rdfs:label ?name;
      <http://rdf.histograph.io/originatedFrom> ?plaats].
  bind(replace(str(?beroep),'http://archief.nl/NL-HaNA/id/beroep/','') as ?beroepStr)
  optional {
    ?personVer
      opvarenden:voynumberdas ?tripID2;
      opvarenden:fullname ?name ;
      opvarenden:rank ?rang;
      opvarenden:ExternalHiscoIdentifier ?hisco;
      opvarenden:ExternalIdentifier ?geo;
      opvarenden:placeorigin ?place;
      opvarenden:scanpermalink ?handle.
    optional {
      ?personVer
        opvarenden:lon_arcgis ?lon;
        opvarenden:lat_arcgis ?lat.
    }
    ?hisco
      a <https://iisg.amsterdam/hisco/code/HISCO>;
      skos:prefLabel ?work.
    bind('<a target="_blank" noreferrer href="https://demo.triply.cc/maritieme-musea/maritieme-musea/browser?resource=' as ?href)
    bind(concat('Volledige naam: ',?href,encode_for_uri(?personVer),'">',?name,'</a><br>',
                'Geboorte Locatie: ',?href,encode_for_uri(?geo),'">',?place,'</a><br>',
                'HISCO: ',?href,encode_for_uri(?hisco),'">',?work,'</a><br>',
                'Rank: ',?rang,'<br>') as ?shapeLabel)
    bind(strdt(concat("Point(",replace(str(?lon),',','.'),' ',replace(str(?lat),',','.'),')'),
               geo:wktLiteral) as ?shape)
    bind(if(strstarts(str(?hisco),'https://iisg.amsterdam/hisco/code/hisco/583'),'green','blue') as ?shapeColor)
  }
}

PREFIX das: <https://demo.triply.cc/huygens-ing/dutch-asiatic-shipping/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX misc: <https://demo.triply.cc/huygens-ing/graph/misc/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ship: <https://demo.triply.cc/huygens-ing/dutch-asiatic-shipping/id/ship/>
PREFIX wgs84: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT
  ?departurePoint (sample(?departurePointLabelNotSampled) as ?departurePointLabel)
  ?arrivalPoint (sample(?arrivalPointLabelNotSampled) as ?arrivalPointLabel)
  ?shape3 (sample(?shape3LabelNotSampled) as ?shape3Label) ?shape3Color
  ?restPoint (sample(?restPointLabelNotSampled) as ?restPointLabel)
{
  bind(ship:DAS_ship0010 as ?ship)# 't Vliegend Hart' == DAS_ship0010
  # voyage
  ?voy das:shipid ?ship;
       das:voyarrivalplaceid ?arrivalId;
       das:voydepartureplaceid ?departureId.
  # departure
  ?departureId das:toponym_original ?departureName;
               owl:sameAs [ wgs84:long ?lonDep; wgs84:lat ?latDep ].
  optional {
    ?havenaangezicht2 misc:place ?departureName;
                      foaf:depiction ?departureImage.
    optional{
      filter(strstarts(str(?departureImage),"http://hdl.handle.net"))
	    ?agg edm:isShownAt ?departureImage;
             edm:isShownBy ?UpdateImageDep.
    }
  }
  bind(strdt(concat("POINT (",?lonDep," ",?latDep,")"),geo:wktLiteral) as ?departurePoint)
  # arrival
  ?arrivalId das:toponym_original ?arrivalName;
             owl:sameAs [ wgs84:long ?lonArr; wgs84:lat ?latArr ].
  optional {
    ?havenaangezicht1 misc:place ?arrivalName;
                      foaf:depiction ?arrivalImage.
    optional {
      filter(strstarts(str(?arrivalImage),"http://hdl.handle.net"))
      ?agg edm:isShownAt ?arrivalImage;
           edm:isShownBy ?UpdateImageArr.
    }
  }
  BIND(strdt(concat("POINT (",?lonArr," ",?latArr,")"),geo:wktLiteral) as ?arrivalPoint)
  # rest
  BIND(<http://sws.geonames.org/3367577/> as ?geoIdKaap)
  ?geoIdKaap wgs84:long ?longKaap; wgs84:lat ?latKaap.
  BIND(strdt(concat("POINT (",?longKaap," ",?latKaap,")"),geo:wktLiteral) as ?restPoint)
  OPTIONAL {
    ?havenaangezicht4 misc:ExternalGeoIdentifier ?geoIdKaap;
      	              misc:place ?kaapName;
                      foaf:depiction ?imagekaap.
  }
  # geometries
  BIND(IF( xsd:integer(?latDep) > 45, strdt(concat("LINESTRING (",?lonDep," ",?latDep,""",
  3.07966  51.77924 ,
  1.34205  50.87258 ,
  -2.08131 49.93115 ,
  -5.33326 48.67037 ,
  -9.77174 43.27050 ,
  -11.44166 36.68907,
  -14.78151 28.71317,
  -19.00026 18.92025,
  -23.87345 12.6475,
  -30.5023 -10.39992,
  -33.11856 -18.33260,
  -31.73428 -27.76883,
  -29.9325 -32.07113,
  -27.9500 -34.08019,
  -26.13816 -35.75632,
  -23.80906 -37.13492,
  -20.90867 -37.65861,
  -15.32761 -37.86705,
  -6.80222 -37.41468,
  2.64602 -36.71335,
  8.0073 -36.1831,
  12.36839 -36.07712,
  """
  ,?longKaap," ",?latKaap,
  """,
  18.35680 -35.39930,
  20.55277 -36.59887,
  26.11185 -37.56297,
  36.15953 -37.76297,
  44.45902 -38.03175,
  72.07865 -38.13552,
  97.47012 -38.35985,
  100.16931 -37.15799,
  102.45447 -35.40458,
  103.81677 -33.19029,
  104.45398 -29.83812,
  105.09119 -24.40449,
  104.82752 -17.17519,
  103.15760 -11.10593,
  """,
  ?lonArr," ",?latArr,")"),geo:wktLiteral),
  strdt(concat("LINESTRING (",?lonDep," ",?latDep,""",
  74.5229 7.69870,
  71.78581 7.03825,
  69.14909 5.55316,
  65.91911 2.85805,
  62.66716 -0.83214,
  60.38200 -4.60650,
  57.21794 -11.21877,
  54.93278 -16.42287,
  52.82341 -21.89969,
  49.25455 -27.66664,
  42.75065 -31.18710,
  36.07096 -33.70848,
  28.51237 -36.15797,
  21.65690 -37.14512,
  """
  ,?longKaap," ",?latKaap,
  """,
  17.03490 -34.35846,
  14.92553 -30.50223,
  11.11303 -23.29844,
  5.65209  -15.34412,
  -8.80494 2.39809,
  -17.59401 10.08075,
  -22.00026 16.52025,
  -28.7247 30.78764,
  -26.85550 37.54813,
  -21.58206 43.00792,
  -17.01175 46.65251 ,
  -12.0898 48.75189 ,
  -6.03727 49.37919 ,
  -0.956103 50.02224 ,
  2.53750 51.63169,
  """,
  ?lonArr," ",?latArr,")"),geo:wktLiteral)) as ?shape3)
  # dates
  ?voy  das:voydepartureedtf ?depDate;
        das:voycapedepartureedtf ?depCapeDate;
        das:voyarrivaldateedtf ?arrDate;
        das:voycapearrivaledtf ?arrCapeDate.
  BIND( concat("Vertrokken van ",?departureName,": ",?depDate, "<br>",
               "Aangekomen op ",?kaapName,": ", ?arrCapeDate, "<br>",
               "Vertrokken van ",?kaapName,": ", ?depCapeDate, "<br>",
               "Aangekomen in ",?arrivalName,": ", ?arrDate, "<br>",
      "Duur van de reis: ", ((?arrDate - ?depDate)/(24*60*60)), " dagen" ) as ?shape3LabelNotSampled)
  # images
  BIND(concat('<h2>',?departureName,'</h2>',
      if(strends(str(?departureImage), ".jpg"),
         concat('<img src="',?departureImage,'" style="width:300px;height:300px;">'),
         if(strlen(str(?UpdateImageDep)) > 1,
            concat('<img src="',?UpdateImageDep,'" style="width:300px;height:300px;">'),
            "Geen afbeelding beschikbaar."))) as ?departurePointLabelNotSampled)
  BIND(concat('<h2>',?arrivalName,'</h2>',
      if(strends(str(?arrivalImage), ".jpg"),
         concat('<img src="',?arrivalImage,'" style="width:300px;height:300px;">'),
         if(strlen(str(?UpdateImageArr)) > 1,
            concat('<img src="',?UpdateImageArr,'" style="width:300px;height:300px;">'),
            "Geen afbeelding beschikbaar."))) as ?arrivalPointLabelNotSampled)
  BIND(concat('<h2>',?kaapName,'</h2>',
      if(strends(str(?imagekaap), ".jpg"),
         concat('<img src="',?imagekaap,'" style="width:300px;height:300px;">'),
         if(strlen(str(?UpdateImageKaap)) > 1,
            concat('<img src="',?UpdateImageKaap,'" style="width:300px;height:300px;">'),
            "Geen afbeelding beschikbaar."))) as ?restPointLabelNotSampled)
  BIND(IF(STRENDS(str(?voy),"6"), "green", "red") AS ?shape3Color)
}

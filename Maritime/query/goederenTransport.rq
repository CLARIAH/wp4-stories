PREFIX bgb: <https://demo.triply.cc/huygens-ing/boekhouder-generaal-batavia/>
PREFIX das: <https://demo.triply.cc/huygens-ing/dutch-asiatic-shipping/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX misc: <https://demo.triply.cc/huygens-ing/graph/misc/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rol: <https://demo.triply.cc/huygens-ing/generale-zeemonsterrollen/>
PREFIX schema: <http://schema.org/>
PREFIX ship1: <https://demo.triply.cc/huygens-ing/generale-zeemonsterrollen/id/das_shiprol/>
PREFIX ship2: <https://demo.triply.cc/huygens-ing/boekhouder-generaal-batavia/id/ship/>
PREFIX wgs84: <http://www.w3.org/2003/01/geo/wgs84_pos#>
SELECT
  ?departureShape
  ?arrivalShape
  (concat('<h3>',?departureName," â‡¨ ",?arrivalName,'</h3>',
          '<table style="width:100%" border="1">',
          '<tr><th>Type</th><th>Hoeveelheid</th><th>Waarde</th></tr>',
          group_concat(distinct ?row;separator=""),
          '</table>') as ?departureShapeLabel)
  (sample(?arrivalShapeLabel) as ?arrivalShapeLabel)
{
  BIND(ship2:4415 as ?ship) # 't Vliegend hart == 4415
  BIND(ship1:DAS_ship0010 as ?das_ship) # "t Vliegend hart = DAS_ship0010
  OPTIONAL {
    ?zeerol rol:das_shipid ?das_ship;
            rol:totaal_opvarenden ?total.
    OPTIONAL { ?zeerol rol:zeevarenden_europees ?totalEuro. }
    OPTIONAL { ?zeerol rol:zeevarenden_aziatisch ?totalAsia. }
  }
  ?voy bgb:ship ?ship;
       bgb:voyarrivalplaceid/schema:name ?arrivalName.
  ?havenaangezicht misc:place ?arrivalName;
                   foaf:depiction ?image;
                   misc:ExternalGeoIdentifier ?arrivalGeo.
  OPTIONAL {
    ?agg edm:isShownAt ?image;
         edm:isShownBy ?UpdateImage.
  }
  ?voy bgb:voydepartureplaceid/schema:name ?departureName.
  ?ID2 das:toponym_original ?departureName;
       owl:sameAs ?departureGeo.
  ?cargo bgb:voyageid ?voy;
         bgb:productid/schema:name ?type.
  OPTIONAL { ?cargo bgb:value ?value. }
  OPTIONAL { ?cargo bgb:valuelicht ?value. }
  OPTIONAL {
    ?cargo bgb:quantity ?quantity.
    OPTIONAL { ?cargo bgb:unitid/schema:name ?unit. }
  }
  ?arrivalGeo wgs84:long ?lonArr; wgs84:lat ?latArr.
  ?departureGeo wgs84:long ?lonDep; wgs84:lat ?latDep.
  BIND(str(concat("POLYGON ((",str(?lonArr)," ",str(?latArr),"," ,str(?lonDep)," ",str(?latDep) ,"))")) as ?departureShape)
  BIND(str(concat("POINT (",str(?lonArr)," ",str(?latArr),")")) as ?arrivalShape)
  BIND(IF(BOUND(?total),CONCAT('<br> totaal aantal opvarenden aan boord: ', ?total),"") AS ?arrivalShapeLabelPart1)
  BIND(IF(BOUND(?totalEuro),CONCAT('<br> totaal aantal Europeese opvarenden aan boord: ', ?totalEuro),"") AS ?arrivalShapeLabelPart2)
  BIND(IF(BOUND(?totalAsia),CONCAT('<br> totaal aantal Aziatische opvarenden aan boord: ', ?totalAsia),"") AS ?arrivalShapeLabelPart3)
  BIND(concat("<tr><td>",?type,"</td><td>",?quantity," ",?unit,"</td><td>",?value, "</td></tr>") as ?row)
  BIND(concat('<h2>',?arrivalName,'</h2>',
              if(strends(str(?image), ".jpg"),
                 concat('<br><img src="',?image,'" style="width:300px;height:300px;">'),
                 if(strlen(str(?UpdateImage)) > 1,
                    concat('<img src="',?UpdateImage,'" style="width:300px;height:300px;">'),
                    "Geen afbeelding beschikbaar.")),
              ?arrivalShapeLabelPart1,
              ?arrivalShapeLabelPart2,
              ?arrivalShapeLabelPart3) as ?arrivalShapeLabel)
} GROUP BY ?departureName ?departureShape ?arrivalName ?arrivalShape

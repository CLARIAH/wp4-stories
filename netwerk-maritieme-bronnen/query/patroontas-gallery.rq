prefix dce: <http://purl.org/dc/elements/1.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix edm: <http://www.europeana.eu/schemas/edm/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix hsm: <https://demo.triply.cc/maritieme-musea/Het-Scheepvaartmuseum/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
select * {
  {
    select ?link ?name ?provider {
      ?item
        dce:title ?name;
        dct:isPartOf ?col.
      ?agg
        edm:aggregatedCHO ?item;
        edm:isShownBy ?link;
        edm:provider ?provider.
      filter(lang(?col)='nl' && regex( ?col,"'t Vliegend Hart") && regex(?name,'tas'))
    }
    limit 1
  } union {
    select ?link ?name ?provider {
      ?item dce:title ?name.
      ?agg
        edm:aggregatedCHO ?item;
        edm:isShownBy ?link;
        edm:provider ?provider.
      filter(str(?provider)='Rijksdienst voor het Cultureel Erfgoed' && str(?name)!='Vulmiddel.')
    }
    limit 3
  } union {
    select ?link ?name ?provider {
      ?item
        dce:title ?name;
        dct:isPartOf ?col.
      ?agg
        edm:aggregatedCHO ?item;
        edm:isShownBy ?link;
        edm:provider ?provider.
      filter(str(?provider)='Rijksmuseum' && regex(?col,"'t Vliegend Hart"))
    } limit 3
  } union {
    select ?link ?name ?provider ?linkUnClean {
      ?item
        a <https://demo.triply.cc/maritieme-musea/Het-Scheepvaartmuseum/def/Wrakvonst>;
        hsm:object_name ?name;
        foaf:depiction ?linkUnClean.
      bind(replace(replace(replace(str(?linkUnClean),
                                   'http://www.maritiemdigitaal.nl/index.cfm\\?event=popup.viewimage&img=',
                                   ''),
                           '%3A',
                           ':'),
                   '%2F',
                   '/') as ?link)
      bind('Het Scheepvaartmuseum' as ?provider)
    } limit 3
  }
  bind(strdt('''
    <div style="position: relative;">
      <img src="{{link}}" style="max-width: 400px;">
      <h1 style="position: absolute; top: -11px; left: 0; width: 100%; line-height: 0px;">
        <span style="color: white; font: bold 12px/24px Helvetica, Sans-Serif; line-height: 1.5em; letter-spacing: -1.05px; background: rgb(0, 0, 0); background: rgba(0, 0, 0, 0.7); padding: 2px;">
          {{name}}
        </span>
      </h1>
      <h2 style="position: absolute; bottom: -11px; left: 0; width: 100%;">
        <span style="color: white; font: bold 15px/45px Helvetica, Sans-Serif; letter-spacing: -1.05px; background: rgb(0, 0, 0); background: rgba(0, 0, 0, 0.7); padding: 1px;">
          {{provider}}
        </span>
      </h2>
    </div>''',rdf:HTML)  as ?widget)
}
limit 1

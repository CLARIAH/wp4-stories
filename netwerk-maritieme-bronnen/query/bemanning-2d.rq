prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix na: <http://archief.nl/def/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix opvarenden: <https://demo.triply.cc/huygens-ing/voc-opvarenden-verrijkingen-carrieres/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix voyage: <https://demo.triply.cc/huygens-ing/voc-opvarenden-verrijkingen-carrieres/id/dasTrip/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
select distinct * {
  bind(voyage:2978.2 as ?trip)
  ?person
    opvarenden:voynumberdas ?trip;
    opvarenden:fullname ?name;
    opvarenden:rank ?rankLabel;
    opvarenden:ExternalHiscoIdentifier ?profession;
    opvarenden:ExternalIdentifier ?geo;
    opvarenden:placeorigin ?placeLabel;
    opvarenden:scanpermalink ?handle;
    opvarenden:lon_arcgis ?lon;
    opvarenden:lat_arcgis ?lat.
  ?profession
    a <https://iisg.amsterdam/hisco/code/HISCO>;
    skos:prefLabel ?professionLabel.
  bind('<a target="_blank" noreferrer href="https://demo.triply.cc/maritieme-musea/maritieme-musea/browser?resource=' as ?aOpen)
  bind(strdt('Volledige naam: {{aOpen}}{{encodedPerson)}}">{{name}}</a><br>Geboorteplaats:{{aOpen}}{{encodedGeo}}">{{placeLabel}}</a><br>HISCO: {{aOpen}}{{encodedprofession)}}">{{professionLabel}}</a><br>Rang: {{rankLabel}}<br>', rdf:HTML) as ?shapeLabel)
  bind(encode_for_uri(?person) as  ?encodedPerson)
  bind(encode_for_uri(?geo) as  ?encodedGeo)
  bind(encode_for_uri(?profession) as  ?encodedprofession)
  bind(strdt(concat("Point(",replace(str(?lon),',','.'),' ',replace(str(?lat),',','.'),')'),
             geo:wktLiteral) as ?shape)
  bind(if(strstarts(str(?profession),'https://iisg.amsterdam/hisco/code/hisco/583'),
          'green',
          'blue') as ?shapeColor)
}

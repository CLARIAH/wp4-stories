prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix na: <http://archief.nl/def/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix opvarenden: <https://demo.triply.cc/huygens-ing/voc-opvarenden-verrijkingen-carrieres/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix voyage: <https://demo.triply.cc/huygens-ing/voc-opvarenden-verrijkingen-carrieres/id/dasTrip/>
select distinct ?shape ?shapeColor ?shapeLabel {
  bind(voyage:2978.2 as ?trip)
  ?person
    opvarenden:voynumberdas ?trip;
    opvarenden:fullname ?name;
    opvarenden:rank ?rankLabel;
    opvarenden:ExternalHiscoIdentifier ?profession;
    opvarenden:ExternalIdentifier ?geo;
    opvarenden:placeorigin ?placeLabel;
    opvarenden:scanpermalink ?handle;
    opvarenden:lon_arcgis ?lon;
    opvarenden:lat_arcgis ?lat.
  ?profession
    a <https://iisg.amsterdam/hisco/code/HISCO>;
    skos:prefLabel ?professionLabel.
  bind('<a target="_blank" noreferrer href="https://demo.triply.cc/maritieme-musea/maritieme-musea/browser?resource=' as ?aOpen)
  bind(concat('Volledige naam: ',?aOpen,encode_for_uri(?person),'">',?name,'</a><br>',
              'Geboorteplaats: ',?aOpen,encode_for_uri(?geo),'">',?placeLabel,'</a><br>',
              'HISCO: ',?aOpen,encode_for_uri(?profession),'">',?professionLabel,'</a><br>',
              'Rang: ',?rankLabel,'<br>') as ?shapeLabel)
  bind(strdt(concat("Point(",replace(str(?lon),',','.'),' ',replace(str(?lat),',','.'),')'),
             geo:wktLiteral) as ?shape)
  bind(if(strstarts(str(?profession),'https://iisg.amsterdam/hisco/code/hisco/583'),
          'green',
          'blue') as ?shapeColor)
}

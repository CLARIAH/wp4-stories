prefix das: <https://demo.triply.cc/huygens-ing/dutch-asiatic-shipping/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix misc: <https://demo.triply.cc/huygens-ing/graph/misc/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix schema: <http://schema.org/>
prefix ship: <https://demo.triply.cc/huygens-ing/dutch-asiatic-shipping/id/ship/>
prefix wgs84: <http://www.w3.org/2003/01/geo/wgs84_pos#>
select ?shape ?shape1 ?shape1Label ?shape2 ?shape2Label {
  bind(ship:DAS_ship0010 as ?ship)# 't Vliegend Hart' == DAS_ship0010
  ?ship das:shipname ?shipName.
  ?voyage
    das:shipid ?ship;
    das:voydepartureedtf ?departureDate;
    das:voydepartureplaceid [
      das:toponym_original ?DepName;
      owl:sameAs [
        wgs84:long ?lonDep;
        wgs84:lat ?latDep]].
  filter not exists{ ?voyage das:voyarrivalplace ?arrival. }
  optional {
    ?wreck
      schema:name ?shipName;
      misc:lat ?latWreck;
      misc:lon ?lonWreck;
      misc:locatie ?locatie;
      misc:historie ?description.
  }
  bind(strdt(concat('Point(',?lonDep,' ',?latDep,')'),geo:wktLiteral) as ?shape1)
  bind(concat('Vertrokken van ',?DepName,': ',?departureDate, '<br>') as ?shape1Label)
  bind(strdt(concat('Point(',?lonWreck,' ',?latWreck,')'),geo:wktLiteral) as ?shape2)
  bind(concat('Gezonken op: ',?locatie,'<br> ',?description, '<br>') as ?shape2Label)
  bind(str(concat('Polygon((',?lonWreck,' ',?latWreck,',',?lonDep,' ',?latDep,'))')) as ?shape).
}
